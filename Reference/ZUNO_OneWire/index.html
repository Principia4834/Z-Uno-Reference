<span zunoVersion="v2"></span>
<h1>Zuno OneWire Library</h1>

<span class="function_description">
	Access 1-wire temperature sensors, memory and other chips.
</span>

<span class="function_description_more">
	Биоблетека может работать с любим пином
</span>

<span class="function_description_more">
For more infomation about pins of Z-Uno <a href="http://z-uno.z-wave.me/technical/pinout/">here</a>
</span>

<span class="function_description_reference">
	<ul>
		<li><a href="/Reference/ZUNO_OneWire/OneWire/">OneWire</a></li>
	</ul>
</span>


<span class="function_example">
	<pre>
		<code class="c">
#include "ZUNO_OneWire.h"

#define MY_SERIAL		Serial

OneWire  ds(10);  // on pin 10 (a 4.7K resistor is necessary)

void setup(void) {
	MY_SERIAL.begin(115200);
}

void loop(void) {
	byte i;
	byte present = 0;
	byte type_s;
	byte data[12];
	byte addr[8];
	float celsius, fahrenheit;
	
	if ( !ds.search(addr)) {
		MY_SERIAL.println("No more addresses.");
		MY_SERIAL.println();
		ds.reset_search();
		delay(250);
		return;
	}

	MY_SERIAL.print("ROM =");
	for( i = 0; i < 8; i++) {
		MY_SERIAL.write(' ');
		MY_SERIAL.print(addr[i], HEX);
	}

	if (OneWire::crc8(addr, 7) != addr[7]) {
		MY_SERIAL.println("CRC is not valid!");
		return;
	}
	MY_SERIAL.println();
	
	// the first ROM byte indicates which chip
	switch (addr[0]) {
		case 0x10:
		MY_SERIAL.println("  Chip = DS18S20");  // or old DS1820
		type_s = 1;
		break;
		case 0x28:
		MY_SERIAL.println("  Chip = DS18B20");
		type_s = 0;
		break;
		case 0x22:
		MY_SERIAL.println("  Chip = DS1822");
		type_s = 0;
		break;
		default:
		MY_SERIAL.println("Device is not a DS18x20 family device.");
		return;
	} 

	ds.reset();
	ds.select(addr);
	ds.write(0x44, 1);        // start conversion, with parasite power on at the end
	
	delay(1000);     // maybe 750ms is enough, maybe not
	// we might do a ds.depower() here, but the reset will take care of it.
	
	present = ds.reset();
	ds.select(addr);    
	ds.write(0xBE);         // Read Scratchpad

	MY_SERIAL.print("  Data = ");
	MY_SERIAL.print(present, HEX);
	MY_SERIAL.print(" ");
	for ( i = 0; i < 9; i++) {           // we need 9 bytes
		data[i] = ds.read();
		MY_SERIAL.print(data[i], HEX);
		MY_SERIAL.print(" ");
	}
	MY_SERIAL.print(" CRC=");
	MY_SERIAL.print(OneWire::crc8(data, 8), HEX);
	MY_SERIAL.println();

	// Convert the data to actual temperature
	// because the result is a 16 bit signed integer, it should
	// be stored to an "int16_t" type, which is always 16 bits
	// even when compiled on a 32 bit processor.
	int16_t raw = (data[1] << 8) | data[0];
	if (type_s) {
		raw = raw << 3; // 9 bit resolution default
		if (data[7] == 0x10) {
		// "count remain" gives full 12 bit resolution
		raw = (raw & 0xFFF0) + 12 - data[6];
		}
	} else {
		byte cfg = (data[4] & 0x60);
		// at lower res, the low bits are undefined, so let's zero them
		if (cfg == 0x00) raw = raw & ~7;  // 9 bit resolution, 93.75 ms
		else if (cfg == 0x20) raw = raw & ~3; // 10 bit res, 187.5 ms
		else if (cfg == 0x40) raw = raw & ~1; // 11 bit res, 375 ms
		//// default is 12 bit resolution, 750 ms conversion time
	}
	celsius = (float)raw / 16.0;
	fahrenheit = celsius * 1.8 + 32.0;
	MY_SERIAL.print("  Temperature = ");
	MY_SERIAL.print(celsius);
	MY_SERIAL.print(" Celsius, ");
	MY_SERIAL.print(fahrenheit);
	MY_SERIAL.println(" Fahrenheit");
}
		</code>
	</pre>
</span>  